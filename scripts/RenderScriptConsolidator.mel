// Pre/post render script consolidator
// by Fredrik Averpil, fredrik.averpil [at] gmail.com
//
// Installation: place this MEL script in your MAYA_SCRIPT_PATH
//
// Please note: Any existing MEL in the Maya scene file (Render Globals) is overridden by this consolidation.
//
// Usage:
// Use the MEL function when commandline rendering and feed it any number of scripts. Example:
// Render.exe -preRender "RenderScriptConsolidator({\"C:/preRender.mel\", \"C:/preRender.py\", \"C:/preRender2.py\"})" C:/MayaProject/scenes/MyScene.ma
//
// ------------------ OLD, NEEDS UPDATING ------------------
// Apply to Tractor .alf like this:
// preRenderString = '{pythonPreRenderLoader("' + filepath + '/' + '", "myscript.py");}'
// postRenderString = '{pythonPreRenderLoader("' + filepath + '/' + '", "myscript.py");}'
//  ... + ' -preRender ' + preRenderString + ' -postRender ' + postRenderString + ...
// ------------------ OLD, NEEDS UPDATING ------------------


proc string filename(string $filepath)
{
	string $filename = match( "[^/\\]*$", $filepath );
	return $filename;
}


proc string folderpath( string $filepath )
{
  string $dir = match( "^.*/", $filepath );

  int $sz = size( $dir );
  // Strip off trailing '/'
  //
  if ( ( $sz > 1 ) && ( substring( $dir, $sz, $sz ) == "/" ) ) {
    $dir = substring( $dir, 1, ($sz - 1) );
  }
  return $dir;
}


global proc RenderScriptConsolidator(string $arguments[])
{
	print("------------ PRE/POST-RENDER SCRIPT CONSOLIDATOR START ------------\n");
	print("Attempting to load and execute scripts:\n");

	// For each script file, load it
	for( $item in $arguments){
		//print($item + "\n");
		$filename = filename( $item );
		$folderpath = folderpath( $item );
		if( `match ".py" $filename` != "" ){
			// Assume Python code
			print("\nPython script detected. Executing " + $filename + "\n");
			$modulename = `substitute ".py" $filename ""`;
			python("import sys\nsys.path.append(\"" + $folderpath + "\")\nimport " + $modulename); // Make sure the Pyhton script is executed by itself
			print("\nDone executing " + $filename + "\n");
		}else if( `match ".mel" $filename` != "" ){
			// Assume MEL code
			print("\nMEL script detected. Executing " + $filename + " ...\n");
			string $command = "source \""+$item+"\""; // Make sure the MEL script is executed by itself
			eval $command;
			print("\nDone executing " + $filename + "\n");

		}

	}

	print("------------ PRE/POST-RENDER SCRIPT CONSOLIDATOR END ------------\n");
} 

